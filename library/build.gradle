apply plugin: 'com.android.library'

android {
	Boolean useAndroidX = project.properties['android.useAndroidX'] != null && project.properties['android.useAndroidX'].toBoolean()
	def targetVersion = project.properties['android.targetVersion'] != null ? project.properties['android.targetVersion'].toInteger() : null
	if (targetVersion == null) {
		compileSdkVersion 28
		buildToolsVersion '28.0.3'
	} else {
		compileSdkVersion targetVersion
		buildToolsVersion '28.0.3'
	}

	defaultConfig {
		minSdkVersion 14
		//noinspection OldTargetApi,ExpiredTargetSdkVersion
		targetSdkVersion 28
		versionCode 1303
		versionName "0.0.13post3"
	}

	buildTypes {
		release {
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
		}
	}

	flavorDimensions "support_version", "api"

	productFlavors {
		supportlib {
			dimension "support_version"
			//noinspection ExpiredTargetSdkVersion,OldTargetApi
			targetSdkVersion 28
			versionNameSuffix "-supportlib"
			testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
		}
		androidx {
			dimension "support_version"
			//noinspection ExpiredTargetSdkVersion,OldTargetApi
			targetSdkVersion 28
			versionNameSuffix "-androidx"
			testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
		}
		api25 {
			dimension "api"
			compileSdkVersion 25
			//noinspection ExpiredTargetSdkVersion,OldTargetApi
			targetSdkVersion 25
			versionNameSuffix "-api25"
		}
		api28 {
			dimension "api"
			//noinspection ExpiredTargetSdkVersion,OldTargetApi
			targetSdkVersion 28
			versionNameSuffix "-api28"
		}
	}

	configurations {
		supportlibApi25Implementation {}
		supportlibApi28Implementation {}
		androidxApi28Implementation {}
	}

	variantFilter { variant ->
		def names = variant.flavors*.name
		if (useAndroidX) {
			if (names.contains("supportlib")) {
				setIgnore(true)
			}
		} else if (!useAndroidX && (names.contains("androidx"))) {
			setIgnore(true)
		}
	}

	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}

}

dependencies {
	supportlibApi25Implementation 'com.android.support:appcompat-v7:26.0.1'
	supportlibApi28Implementation 'com.android.support:appcompat-v7:28.0.0'
	androidxApi28Implementation 'androidx.appcompat:appcompat:1.3.1'
}
